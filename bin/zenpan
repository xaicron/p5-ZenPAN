#!perl

use strict;
use warnings;
use feature 'say';
use ZenPAN;
use Capture::Tiny ':all';
use Cwd qw(realpath);
use Cwd::Guard qw(cwd_guard);
use Term::ANSIColor qw(color colored);
use File::Temp qw(tempdir);
use File::Path qw(mkpath);
use File::Which qw(which);
use File::Basename qw(basename);
use Getopt::Long qw(GetOptions);

my $zenpan_cmmand = realpath $0;

# Checking required commands
for my $cmd (qw/cpanm orepan.pl orepan_index.pl git/) {
    which $cmd or die colored(['bold red'], "ERROR: `$cmd` command not found!"), "\n";
}

our $COMMAND_DEPTH = 1;
our $COLOR_MAP     = {
    1 => 'green',
    2 => 'yellow',
    3 => 'cyan',
};

my $default_pause_id = 'DUMMY';

GetOptions(
    'V|version!' => \&show_version,
    'v|verbose!' => \my $verbose,
    'h|help!'    => \&usage,
    'p|path=s'   => \my $path,
) or usage();
usage() unless defined $path;

main: {
    my $cmd = shift @ARGV || 'add';
    my $code = main->can("cmd_$cmd");
    unless ($code) {
        say STDERR "$cmd is not a command";
        usage();
    }

    $code->($path);

    say 'done.';
}

exit;

sub cmd_add {
    my ($path) = @_;
    $path = realpath $path;
    mkpath $path;

    my $targets;
    if (@ARGV) {
        $targets = [ map +{ dist => $_ }, @ARGV ];
    }
    else {
        unless ($targets = challeng_load_argv_from_fh()) {
            usage() unless -f '.zenfile';
            $targets = load_zenfile('.zenfile');
        }
    }

    say "Addting to $path ...";
    for my $target (@$targets) {
        save_dist($path, @$target{qw/dist pause_id/});
    }

    cmd_reindex($path);
}

sub cmd_remove {
    my ($path) = @_;

    # TODO
    ...
}

sub cmd_reindex {
    my ($path) = @_;

    say 'Reindexing...';
    reindex($path);
}

sub cmd_clean {
    my ($path) = @_;

    say 'Cleaning...';
    # TODO
    ...
}

sub load_zenfile {
    my $file = shift;
    open my $fh, '<', $file or die "$!: $file";
    parse_from_fh($fh);
}

sub challeng_load_argv_from_fh {
    return unless $0 ne '-' && !-t STDIN;
    parse_from_fh(*STDIN);
}

sub parse_from_fh {
    my $fh = shift;

    my $targets = [];
    while (defined (my $line = readline $fh)) {
        chomp $line;
        $line =~ s/^\s+|\s+$//g;  # trim spaces
        $line =~ s/#.*$//;        # remove trailing comment
        next if $line =~ /^\s*$/; # empry line
        next if $line =~ /^#/;    # comment line

        my ($dist, $pause_id) = split /\s+/, $line;
        push @$targets, {
            dist     => $dist,
            pause_id => $pause_id || $default_pause_id,
        };
    }

    return $targets;
}

sub save_dist {
    my ($path, $dist, $pause_id) = @_;
    $pause_id ||= $default_pause_id;

    say colored ['underline bold'], "Working $dist ...";

    if ($dist =~ m|^https?://\w+\.|) {
        run_cpanm($path, $dist);
    }
    elsif ($dist =~ /tar\.gz$/) {
        run_orepan($path, $pause_id, $dist);
    }
    elsif ($dist =~ /\.git$/) {
        run_git($path, $pause_id, $dist);
    }
    else {
        run_cpanm($path, $dist);
    }
}

sub run {
    my @cmd = @_;
    my $indent = sprintf '%s> ', '==' x $COMMAND_DEPTH;
    my $indent_color = $COLOR_MAP->{$COMMAND_DEPTH} || 'bule';
    say color($indent_color), $indent, color('reset'), join q{ }, @cmd;
    if ($verbose) {
        tee_merged { !system @cmd };
    }
    else {
        capture_merged { !system @cmd };
    }
}

sub run_cpanm {
    my ($path, @args) = @_;

    local $ENV{PERL_CPANM_OPT};
    run cpanm => (
        '-L', $path,
        '--save-dists', $path,
        '--notest',
        '--quiet',
        @args,
    );
}

sub run_orepan {
    my ($path, $pause_id, $dist) = @_;
    run 'orepan.pl' => (
        '--destination', $path,
        '--pause', $pause_id,
        $dist,
    );
}

sub run_perl {
    my (@args) = @_;
    local $ENV{PERL_CPANM_OPT};
    run $^X, @args;
}

sub run_zenpan {
    my ($path) = @_;
    run_perl $zenpan_cmmand, '-p', $path, 'add';
}

sub run_git {
    my ($path, $pause_id, $repos) = @_;
    my $dir = tempdir CLEANUP => 1;
    my $guard = cwd_guard $dir or die "$!: $dir";

    run 'git', 'clone', $repos, "$dir/temp$$";
    my $guard2 = cwd_guard "temp$$" or die "$!: $dir";

    local $COMMAND_DEPTH = $COMMAND_DEPTH + 1;
    if (-f 'Build.PL') {
        run_perl 'Build.PL';
        unless (-f 'MANIFEST') {
            run_perl './Build', 'manifest';
        }
        if (-f '.zenfile') {
            run_zenpan $path;
        }
        run_cpanm $path, '--installdeps', '.';
        run_perl './Build', 'dist';
    }
    elsif (-f 'Makefile.PL') {
        run_perl 'Makefile.PL';
        unless (-f 'MANIFEST') {
            run 'make', 'manifest';
        }
        if (-f '.zenfile') {
            run_zenpan $path;
        }
        run_cpanm $path, '--installdeps', '.';
        run 'make', 'dist';
    }
    else {
        say STDERR colored ['bold red'], "ERROR: $repos not perl module repository";
        return;
    }

    my ($dist) = <*.tar.gz>;
    unless (-f $dist) {
        say STDERR colored ['bold red'], "ERROR: cannot create dist file from $repos";
        return;
    }

    local $COMMAND_DEPTH = $COMMAND_DEPTH + 1;
    run_orepan($path, $pause_id, $dist);
}

sub reindex {
    my $path = shift;
    unlink "$path/modules/02packages.details.txt.gz";
    run 'orepan_index.pl', '--repository', $path;
}

sub usage {
    my $me = basename $0;
    print << "USAGE";
Usage: $me [command] [option] ...

Options:
  --path,    -p  Save archives path. (required)
  --help,    -h  Show this message.
  --verbose, -v  Chatty print,
  --version, -V  Show version.

Command:
  add        Additional into local mirror path
  remove     Removeing modules from local mirror path
  reindex    Regen index file

Examples:

  % zenpan add -p ./mirror Moo
  % zenpan add -p ./mirror < zenfile

  % cat .zenfile
  # git URL
  git\@github.com:xaicron/p5-DBIx-QueryLog.git XAICRON # authors is XAICRON
  git\@github.com:tokuhirom/Furl.git TOKUHIROM         # authors is TOKUHIROM
  https://github.com/nekokak/p5-DBIx-Handler.git      # authors is DUMMY

  # tarballs URL
  http://cpan.metacpan.org/authors/id/X/XA/XAICRON/App-envfile-0.06.tar.gz

  # module name
  SQL::Format
  WWW::Google::Cloud::Messaging~0.03 # version specify
  % zenpan add -p ./mirror # using .zenfile

  # remove modules
  % zepnan remove -p ./mirror SQL::Format
  % zenpan remove -p ./mirror XAICRON/App-envfile-0.06.tar.gz
  % zepnan remove -p ./mirror WWW::Google::Cloud::Messaging~0.03

  # reindex
  % zenpan reindex -p ./mirror

`.zenfile` ditails are SEE ALSO "perldoc ZenPAN".

USAGE

    exit;
}

sub show_version {
    my $me = basename $0;
    print << "SHOW_VERSION";
$me (ZenPAN) version $ZenPAN::VERSION
SHOW_VERSION
    exit;
}

__END__

=encoding utf8

=head1 NAME

zenpan - yet yet another DarkPAN repository manager.

=head1 SYNOPSIS

  # create local mirror
  % zenpan add -p ./mirror Moo
  % zenpan add -p ./mirror < zenfile

  % cat .zenfile
  # git URL
  git@github.com:xaicron/p5-DBIx-QueryLog.git XAICRON # authors is XAICRON
  git@github.com:tokuhirom/Furl.git TOKUHIROM         # authors is TOKUHIROM
  https://github.com/nekokak/p5-DBIx-Handler.git      # authors is DUMMY

  # tarballs URL
  http://cpan.metacpan.org/authors/id/X/XA/XAICRON/App-envfile-0.06.tar.gz

  # module name
  SQL::Format
  WWW::Google::Cloud::Messaging~0.03 # version specify
  % zenpan add -p ./mirror # using .zenfile

  # remove modules
  % zepnan remove -p ./mirror SQL::Format
  % zenpan remove -p ./mirror XAICRON/App-envfile-0.06.tar.gz
  % zepnan remove -p ./mirror WWW::Google::Cloud::Messaging~0.03

  # reindex
  % zenpan reindex -p ./mirror

  # and use it
  % cpanm --mirror-only --mirror=file:///path/to/mirror SQL::Format

I<< zenfile >> foramt details are SEE ALSO C<< perldoc ZenPAN >>.

=head1 DESCRIPTION

ZenPAN is simple wrapper of L<< OrePAN >> and L<< cpanm >>.
Easy to create a DarkPAN from I<< git >>, I<< tarball >>, and I<< CPAN >>.

=head1 AUTHOR

Yuji Shimada E<lt>xaicron {@} GMAIL.COME<gt>

=head1 COPYRIGHT

Copyright 2013 - Yuji Shimada

=head1 LICENSE

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=head1 SEE ALSO

=cut
